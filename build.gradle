plugins {
    id 'java-library'
    id 'maven-publish'
    id 'idea'
    id 'net.neoforged.moddev' version '2.0.82'
    id 'io.freefair.lombok' version '8.6'
    id("dev.vfyjxf.modaccessor") version "1.1"
}

version = mod_version
group = mod_group_id

repositories {
    mavenLocal()
    mavenCentral()

    //kubejs
    maven {
        name = "Latvian"
        url = "https://maven.latvian.dev/releases"
        content {
            includeGroup "dev.latvian.mods"
            includeGroup "dev.latvian.apps"
        }
    }
    maven {
        name = "JitPack"
        url = "https://jitpack.io"
        content {
            includeGroup "com.github.rtyley"
        }
    }

    // LDLib2
    maven {
        name = "Jared's maven"
        url = "https://maven.firstdarkdev.xyz/snapshots"
    }

    //GeckoLib
    maven {
        name = 'GeckoLib'
        url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/'
        content {
            includeGroupByRegex("software\\.bernie.*")
            includeGroup("com.eliotlash.mclib")
        }
    }

    //Modrinth
    maven {
        name = "Modrinth"
        url = "https://api.modrinth.com/maven"
    }

    //cf
    maven {
        url "https://cursemaven.com"
    }
}

base {
    archivesName = mod_id
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

configurations {
    create("mixinAgent") {
        canBeConsumed = false
        canBeResolved = true
        defaultDependencies {
            add(dependencyFactory.create("dev.vfyjxf:mixin-hotswap-agent:1.1").setTransitive(false))
        }
    }
}


neoForge {
    version = project.neo_version

    parchment {
        mappingsVersion = project.parchment_mappings_version
        minecraftVersion = project.parchment_minecraft_version
    }

    runs {
        client {
            client()

            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        clinet2 {
            client()

            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
            programArgument '--username'
            programArgument 'zhenshiz'
        }

        server {
            server()
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        gameTestServer {
            type = "gameTestServer"
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        data {
            data()
            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }

        configureEach {
            def files = configurations.mixinAgent.files
            if (!files.isEmpty()) {
                jvmArgument '-javaagent:' + files.first().toPath()
            }
            systemProperty 'forge.logging.markers', 'REGISTRIES'

            jvmArguments.add '-XX:+AllowEnhancedClassRedefinition'
            jvmArguments.add '-XX:HotswapAgent=fatjar'

            logLevel = org.slf4j.event.Level.DEBUG
        }
    }

    mods {
        "${mod_id}" {
            sourceSet(sourceSets.main)
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

modAccessor {
    createTransformConfiguration(configurations.compileOnly)
    accessTransformerFiles = project.files('src/main/resources/META-INF/accesstransformer.cfg')
}

dependencies {
    //前置模组
    // LDLib2
    implementation("com.lowdragmc.ldlib2:ldlib2-neoforge-${minecraft_version}:${ldlib2_version}:all") { transitive = false }
    implementation "org.appliedenergistics.yoga:yoga:1.0.0"

    //联动模组
    // KubeJS
    implementation "dev.latvian.mods:rhino:${rhino_version}"
    implementation "dev.latvian.mods:kubejs-neoforge:${kubejs_version}"

    //测试用模组
    implementation "maven.modrinth:sodium:${sodium_version}"
    implementation "maven.modrinth:iris:${iris_version}"
    implementation "maven.modrinth:lan-server-properties:${lsp_version}"
    implementation "maven.modrinth:jei:${jei_version}"

    //plugin
    additionalRuntimeClasspath("dev.vfyjxf:mixin-hotswap-agent:1.1")
}

tasks.withType(ProcessResources).configureEach {
    var replaceProperties = [minecraft_version      : minecraft_version,
                             minecraft_version_range: minecraft_version_range,
                             neo_version            : neo_version,
                             neo_version_range      : neo_version_range,
                             loader_version_range   : loader_version_range,
                             mod_id                 : mod_id,
                             mod_name               : mod_name,
                             mod_license            : mod_license,
                             mod_version            : mod_version,
                             mod_authors            : mod_authors,
                             mod_description        : mod_description]
    inputs.properties replaceProperties

    filesMatching(['META-INF/neoforge.mods.toml']) {
        expand replaceProperties
    }
}

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/repo"
        }
    }
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}
